apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: ResourceHashPlugin

def env
if (hasProperty('env')) {
    env = property('env')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {
    providedCompile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.glassfish.jersey.test-framework:jersey-test-framework-core:2.19'
    testRuntime 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-bundle:2.19'
}

test {
    systemProperty 'java.util.logging.config.file', 'logging.properties'

    def testFactoryClass
    switch(env) {
        case 'grizzly' : testFactoryClass = 'org.glassfish.jersey.test.grizzly.GrizzlyTestContainerFactory'      ; break
        case 'inmemory': testFactoryClass = 'org.glassfish.jersey.test.inmemory.InMemoryTestContainerFactory'    ; break
        case 'jdk'     : testFactoryClass = 'org.glassfish.jersey.test.jdkhttp.JdkHttpServerTestContainerFactory'; break
        case 'jetty'   : testFactoryClass = 'org.glassfish.jersey.test.jetty.JettyTestContainerFactory'          ; break
        case 'simple'  : testFactoryClass = 'org.glassfish.jersey.test.simple.SimpleTestContainerFactory'        ; break
    }
    if (testFactoryClass) {
        systemProperty 'jersey.config.test.container.factory', testFactoryClass
    }
}

class ResourceHashPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.pluginManager.apply(JavaPlugin.class)
        project.tasks.create('hash', ResourceHashTask.class)
        project.classes.dependsOn project.hash
        project.hash.dependsOn project.processResources
    }
}

class ResourceHashTask extends DefaultTask {
    def algorithm = 'MD5'
    def resources = project.fileTree(project.processResources.destinationDir)
    @TaskAction
    def hash() {
        def md = java.security.MessageDigest.getInstance(algorithm)
        resources.visit {
            def src = it.file
            if (src.file) {
                def file = new File(src.parent, src.name + '.' + algorithm.toLowerCase())
                def digest = md.digest(src.bytes).collect { String.format('%02x', it) }.join()
                file.write(digest)
            }
        }
    }
}

